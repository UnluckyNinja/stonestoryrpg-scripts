var lc = import MyScripts/locheck
var rsc = import MyScripts/resources
var eq = import MyScripts/equips
var pot = import MyScripts/potions

?lc.isMine()
  pot.usehealthPotion()
  eq.equipWalk()
  ?loc.stars <= 5
    equipL triskelion
  rsc.pickups()
  rsc.harvests()

  ?foe.distance < 30
    ?foe = boss
      equipBoss()
    :
      ?foe = mine_walker & foe.distance > 9
        eq.equipDash()
        equipL long sword
      :?loc.stars <= 5
        eq.equipSpeed()
      :
        eq.equipGeneral()

  ?foe = boss
    ?foe.state = 32
      ?loc.stars > 5
        ?foe.time = 32
          equipR mind stone
      :
        ?foe.time = 25
          equipR mind stone
    :?foe.state = 4
      eq.equipFarm()

func equipBoss()
  ?eq.isUsingSkill()
    return
  ?foe = boss & (foe.distance < 9) & eq.tryAblities()
    return
  ?foe.state = 33
    ?foe.distance > 7
      eq.staffDash()
      equipR dashing shield
      equipL vigor sword dL
    :?foe.armor > 0
      equipR aether hammer D *6
      equipL aether hammer D *5
    :
      equipR aether sword D -long
      equipL aether sword dU -long
  :
    equip aether socketed crossbow D