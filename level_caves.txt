var lc = import MyScripts/locheck
var rsc = import MyScripts/resources
var eq = import MyScripts/equips
var pot = import MyScripts/potions

?lc.isCave()
  pot.usehealthPotion()
  eq.equipWalk()
  ?loc.stars <= 5
    equipL triskelion
  rsc.pickups()
  rsc.harvests()

  ?foe.distance < 23
    ?foe = boss
      equipBoss()
    :
      ?loc.stars <= 5
        eq.equipSpeed()
      :
        eq.equipGeneral()

  ?foe = boss
    ?foe.state = 142 & item.GetCooldown("mind") <= 0
      ?foe.time = 45
        equipR mind stone
    :?foe.state = 4
      eq.equipFarm()

func equipBoss()
  ?eq.isUsingSkill()
    return
  ?foe.distance > 5 // get close
    equipR dashing shield
    equipL sword
    return
  ?foe.distance < 9
    ?eq.tryAblities()
      return
  ?foe.armor > 0
    equipR hammer
  :?foe.debuffs.string ! debuff_damage
    equipR poison sword dP
  :?foe.debuffs.string ! debuff_dot
    equipR fire sword dF
  :
    equipR ice sword dI
    
  ?hp < maxhp
    equipL vigor sword dL
  :
    equipL ice sword D